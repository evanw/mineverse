@entry
def main {
  const FRAME_SKIP_COUNT = 1 # 30 fps is better than a ragged 60fps since Chrome drops frames sometimes anyway
  const SHIFT_KEY = 16

  var oldTime = 0.0
  var pressed = false
  var oldMouse dynamic = null
  var keys IntMap<int> = {}
  var app = App.new
  var element = app.element
  var renderTextureMap = true
  var renderGodrays = true
  var renderFog = true
  var tick fn()
  var blur = => {
    keys = {}
    pressed = false
  }
  var keydown = (e dynamic) => {
    keys[e.which] = 1
    if e.which == 'T' { renderTextureMap = !renderTextureMap }
    else if e.which == 'G' { renderGodrays = !renderGodrays }
    else if e.which == 'F' { renderFog = !renderFog }
  }
  var keyup = (e dynamic) => {
    keys[e.which] = 0
  }
  var wheel = (e dynamic) => {
    if e.ctrlKey {
      app.moveCamera(0, e.deltaY * 0.1)
    } else {
      app.rotateCamera(e.deltaX * -(Math.PI / 400), e.deltaY * -(Math.PI / 400)) # OS X trackpad
    }
    e.preventDefault()
  }
  var mousedown = (e dynamic) => {
    pressed = true
    e.preventDefault()
  }
  var mouseup = => {
    pressed = false
  }
  var mousemove = (e dynamic) => {
    if pressed {
      var deltaX = oldMouse != null ? e.pageX - oldMouse.pageX : 0
      var deltaY = oldMouse != null ? e.pageY - oldMouse.pageY : 0
      app.rotateCamera(deltaX * (Math.PI / 400), deltaY * (Math.PI / 400))
    }
    oldMouse = e
  }
  var resize = => {
    app.resize(window.innerWidth, window.innerHeight)
  }
  var count = 0
  tick = => {
    count++
    if count % (1 + FRAME_SKIP_COUNT) == 0 {
      var newTime double = window.performance.now()
      var seconds = Math.min(0.25, (newTime - oldTime) / 1000)
      oldTime = newTime

      var speed = (4 + (20 - 4) * keys.get(SHIFT_KEY, 0)) * seconds
      var deltaX = (keys.get('D', 0) - keys.get('A', 0)) * speed
      var deltaY = (keys.get('S', 0) - keys.get('W', 0)) * speed
      app.moveCamera(deltaX, deltaY)

      app.renderTextureMap = renderTextureMap
      app.renderGodrays = renderGodrays
      app.renderFog = renderFog
      app.tick(seconds)
      document.title = app.title
    }
    window.requestAnimationFrame(tick)
  }
  resize()
  tick()

  var instructions = document.createElement("div")
  var style = instructions.style
  style.position = "absolute"
  style.left = "20px"
  style.top = "20px"
  style.padding = "13px 15px"
  style.background = "rgba(0, 0, 0, 0.5)"
  style.color = "white"
  style.pointerEvents = "none"
  style.whiteSpace = "pre"
  style.font = "14px/20px sans-serif"
  style.borderRadius = "5px"
  instructions.textContent =
    "W/A/S/D: Move\n" +
    "Shift: Move fast\n" +
    "Drag: Rotate\n" +
    "T: Toggle textures\n" +
    "G: Toggle godrays\n" +
    "F: Toggle fog"

  document.body.appendChild(element)
  document.body.appendChild(instructions)
  document.onkeydown = keydown
  document.onkeyup = keyup
  window.onblur = blur
  window.onresize = resize
  window.onmousedown = mousedown
  window.onmouseup = mouseup
  window.onmousemove = mousemove
  window.onwheel = wheel
}
