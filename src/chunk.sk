enum Cell {
  EMPTY
  DIRT
  GRASS
  STONE
  SNOW
  METAL
}

enum Direction {
  XNEG
  YNEG
  ZNEG
  XPOS
  YPOS
  ZPOS

  def deltaX int {
    return self == XNEG ? -1 : self == XPOS ? 1 : 0
  }

  def deltaY int {
    return self == YNEG ? -1 : self == YPOS ? 1 : 0
  }

  def deltaZ int {
    return self == ZNEG ? -1 : self == ZPOS ? 1 : 0
  }

  def opposite Direction {
    return ((self + 3) % 6) as Direction
  }
}

enum Face {
  TOP
  BOTTOM
  SIDES
}

class Chunk {
  const _terrain Terrain
  const _group dynamic
  const _depthGroup dynamic
  const _updateGroup dynamic
  const _material dynamic
  const _startX int
  const _startY int
  const _startZ int
  const _data = Uint8Array.new(SIZE_X * SIZE_Y * SIZE_Z)
  const _neighbors List<Chunk> = [null, null, null, null, null, null]
  var _outerBox = THREE.Box2.new()
  var _innerBox = THREE.Box2.new()
  var _vertices List<double> = null
  var _uvs List<double> = null
  var _updateUVs List<double> = null
  var _updatePosition List<double> = null
  var _indices List<int> = null
  var _quads List<int> = null
  var _quadIndices IntMap<int> = null
  var _quadPreviousIndices IntMap<int> = {}
  var _geometry dynamic = null
  var _mesh dynamic = null
  var _depthMesh dynamic = null
  var _updateMesh dynamic = null
  var _isInvalid = false

  def destroy {
    _destroyMeshesAndGeometry

    # Unlink from neighbors
    for i in 0..6 {
      var neighbor = _neighbors[i]
      if neighbor != null {
        var opposite = (i as Direction).opposite
        assert(neighbor._neighbors[opposite] == self)
        neighbor._neighbors[opposite].invalidate
        neighbor._neighbors[opposite] = null
      }
    }

    # Free all quads
    if _quads != null {
      for quad in _quads {
        _terrain.lightmap.free(quad)
      }
      _quads = null
      _quadIndices = null
      _quadPreviousIndices = {}
    }
  }

  def _destroyMeshesAndGeometry {
    if _mesh != null {
      _group.remove(_mesh)
      _mesh = null
    }

    if _depthMesh != null {
      _depthGroup.remove(_depthMesh)
      _depthMesh = null
    }

    if _updateMesh != null {
      _updateGroup.remove(_updateMesh)
      _updateMesh = null
    }

    if _geometry != null {
      _geometry.dispose()
      _geometry = null
    }
  }

  def key int {
    return Terrain.createKey(_startX, _startY, _startZ)
  }

  def startX double {
    return _startX
  }

  def startY double {
    return _startY
  }

  def startZ double {
    return _startZ
  }

  def data Uint8Array {
    return _data
  }

  def geometry Uint8Array {
    return _geometry
  }

  def isInvalid bool {
    return _isInvalid
  }

  def invalidate {
    _isInvalid = true
  }

  def neighbor(direction Direction) Chunk {
    return _neighbors[direction]
  }

  def contains(x int, y int, z int) bool {
    x -= _startX
    y -= _startY
    z -= _startZ
    return
      x >= 0 && x < SIZE_X &&
      y >= 0 && y < SIZE_Y &&
      z >= 0 && z < SIZE_Z
  }

  def linkTo(other Chunk, direction Direction) {
    assert(other._startX == _startX + direction.deltaX * SIZE_X)
    assert(other._startY == _startY + direction.deltaY * SIZE_Y)
    assert(other._startZ == _startZ + direction.deltaZ * SIZE_Z)

    assert(_neighbors[direction] == null)
    assert(other._neighbors[direction.opposite] == null)

    _neighbors[direction] = other
    other._neighbors[direction.opposite] = self

    invalidate
    other.invalidate
  }

  def get(x int, y int, z int) Cell {
    if contains(x, y, z) {
      return _data[(x - _startX) + ((y - _startY) + (z - _startZ) * SIZE_Y) * SIZE_X] as Cell
    }
    return .EMPTY
  }

  def getWithNeighbors(x int, y int, z int) Cell {
    var chunk = self

    if x < chunk._startX && chunk.neighbor(.XNEG) != null { chunk = chunk.neighbor(.XNEG) }
    else if x >= chunk._startX + SIZE_X && chunk.neighbor(.XPOS) != null { chunk = chunk.neighbor(.XPOS) }

    if y < chunk._startY && chunk.neighbor(.YNEG) != null { chunk = chunk.neighbor(.YNEG) }
    else if y >= chunk._startY + SIZE_Y && chunk.neighbor(.YPOS) != null { chunk = chunk.neighbor(.YPOS) }

    if z < chunk._startZ && chunk.neighbor(.ZNEG) != null { chunk = chunk.neighbor(.ZNEG) }
    else if z >= chunk._startZ + SIZE_Z && chunk.neighbor(.ZPOS) != null { chunk = chunk.neighbor(.ZPOS) }

    return chunk.get(x, y, z)
  }

  def set(x int, y int, z int, cell Cell) {
    if contains(x, y, z) {
      x -= _startX
      y -= _startY
      z -= _startZ
      _data[x + (y + z * SIZE_Y) * SIZE_X] = cell
      invalidate

      # Also invalidate neighboring chunks
      if x == 0 && neighbor(.XNEG) != null { neighbor(.XNEG).invalidate }
      if y == 0 && neighbor(.YNEG) != null { neighbor(.YNEG).invalidate }
      if z == 0 && neighbor(.ZNEG) != null { neighbor(.ZNEG).invalidate }
      if x == SIZE_X - 1 && neighbor(.XPOS) != null { neighbor(.XPOS).invalidate }
      if y == SIZE_Y - 1 && neighbor(.YPOS) != null { neighbor(.YPOS).invalidate }
      if z == SIZE_Z - 1 && neighbor(.ZPOS) != null { neighbor(.ZPOS).invalidate }
    }
  }

  def update {
    if !_isInvalid {
      return
    }

    _destroyMeshesAndGeometry
    _quads = []
    _quadIndices = {}
    _vertices = []
    _uvs = []
    _updateUVs = []
    _updatePosition = []
    _indices = []

    _generateGeometry

    # Free all unused quads
    for quad in _quadPreviousIndices.values {
      _terrain.lightmap.free(quad)
    }
    _quadPreviousIndices = _quadIndices
    _quadIndices = null

    # Upload the data to the GPU
    _geometry = THREE.BufferGeometry.new()
    _geometry.setIndex(THREE.BufferAttribute.new(Uint16Array.new(_indices), 1))
    _geometry.addAttribute("position", THREE.BufferAttribute.new(Float32Array.new(_vertices), 3))
    _geometry.addAttribute("uvs", THREE.BufferAttribute.new(Float32Array.new(_uvs), 4))
    _geometry.addAttribute("updateUV", THREE.BufferAttribute.new(Float32Array.new(_updateUVs), 2))
    _geometry.addAttribute("updatePosition", THREE.BufferAttribute.new(Float32Array.new(_updatePosition), 3))

    # Add the new mesh
    _mesh = THREE.Mesh.new(_geometry, _material)
    _depthMesh = THREE.Mesh.new(_geometry, _terrain.lightmap.depthMaterial)
    _updateMesh = THREE.Mesh.new(_geometry, _terrain.lightmap.updateMaterial)
    _group.add(_mesh)
    _depthGroup.add(_depthMesh)
    _updateGroup.add(_updateMesh)
    _updateMesh.frustumCulled = false

    # Allow these to be garbage collected
    _vertices = null
    _uvs = null
    _updateUVs = null
    _updatePosition = null
    _indices = null

    _isInvalid = false
  }

  def _generateGeometry {
    for x in _startX.._startX + SIZE_X + 1 {
      for y in _startY.._startY + SIZE_Y + 1 {
        for z in _startZ.._startZ + SIZE_Z + 1 {
          var quadBaseIndex = (x + (y + z * (SIZE_Y + 1)) * (SIZE_X + 1)) * 6

          # Draw quads on the x axis
          if y < _startY + SIZE_Y && z < _startZ + SIZE_Z {
            var neg = getWithNeighbors(x - 1, y, z)
            var pos = getWithNeighbors(x, y, z)

            if neg == .EMPTY && pos != .EMPTY {
              _appendQuad(x, y + 1, z + 1, 0, 0, -1, 0, -1, 0, TEXTURE_INDEX[pos][Face.SIDES], quadBaseIndex)
            }

            else if neg != .EMPTY && pos == .EMPTY {
              _appendQuad(x, y + 1, z, 0, 0, 1, 0, -1, 0, TEXTURE_INDEX[neg][Face.SIDES], quadBaseIndex + 1)
            }
          }

          # Draw quads on the y axis
          if x < _startX + SIZE_X && z < _startZ + SIZE_Z {
            var neg = getWithNeighbors(x, y - 1, z)
            var pos = getWithNeighbors(x, y, z)

            if neg == .EMPTY && pos != .EMPTY {
              _appendQuad(x, y, z, 1, 0, 0, 0, 0, 1, TEXTURE_INDEX[pos][Face.BOTTOM], quadBaseIndex + 2)
            }

            else if neg != .EMPTY && pos == .EMPTY {
              _appendQuad(x + 1, y, z, -1, 0, 0, 0, 0, 1, TEXTURE_INDEX[neg][Face.TOP], quadBaseIndex + 3)
            }
          }

          # Draw quads on the z axis
          if x < _startX + SIZE_X && y < _startY + SIZE_Y {
            var neg = getWithNeighbors(x, y, z - 1)
            var pos = getWithNeighbors(x, y, z)

            if neg == .EMPTY && pos != .EMPTY {
              _appendQuad(x, y + 1, z, 1, 0, 0, 0, -1, 0, TEXTURE_INDEX[pos][Face.SIDES], quadBaseIndex + 4)
            }

            else if neg != .EMPTY && pos == .EMPTY {
              _appendQuad(x + 1, y + 1, z, -1, 0, 0, 0, -1, 0, TEXTURE_INDEX[neg][Face.SIDES], quadBaseIndex + 5)
            }
          }
        }
      }
    }
  }

  def _appendQuad(x int, y int, z int, ax int, ay int, az int, bx int, by int, bz int, textureIndex int, quadIndex int) {
    var cx = x + ax * 0.5 + bx * 0.5
    var cy = y + ay * 0.5 + by * 0.5
    var cz = z + az * 0.5 + bz * 0.5

    var s0 = (1 + textureIndex % 16) / 16.0 - 0.0001
    var t0 = 1 - (textureIndex / 16) / 16.0 - 0.0001
    var s1 = s0 - 1 / 16.0 + 0.0002
    var t1 = t0 - 1 / 16.0 + 0.0002

    var i = _vertices.count / 3
    var indices = _indices
    indices.append(i)
    indices.append(i + 1)
    indices.append(i + 2)
    indices.append(i)
    indices.append(i + 2)
    indices.append(i + 3)

    # Reuse existing lightmap data if this isn't a new quad
    var index = _quadPreviousIndices.get(quadIndex, -1)
    if index == -1 {
      index = _terrain.lightmap.allocate
    } else {
      _quadPreviousIndices.remove(quadIndex)
    }
    var inner = _innerBox
    var outer = _outerBox
    _terrain.lightmap.innerQuadForIndex(index, inner)
    _terrain.lightmap.outerQuadForIndex(index, outer)
    _quads.append(index)
    _quadIndices[quadIndex] = index

    _appendVertex(x,           y,           z,           s0, t0, inner.min.x, inner.min.y, outer.min.x, outer.min.y, cx, cy, cz)
    _appendVertex(x + ax,      y + ay,      z + az,      s1, t0, inner.max.x, inner.min.y, outer.max.x, outer.min.y, cx, cy, cz)
    _appendVertex(x + ax + bx, y + ay + by, z + az + bz, s1, t1, inner.max.x, inner.max.y, outer.max.x, outer.max.y, cx, cy, cz)
    _appendVertex(x +      bx, y +      by, z +      bz, s0, t1, inner.min.x, inner.max.y, outer.min.x, outer.max.y, cx, cy, cz)
  }

  def quadCount int {
    return _quads != null ? _quads.count : 0
  }

  def _appendVertex(x int, y int, z int, s0 double, t0 double, s1 double, t1 double, s2 double, t2 double, cx double, cy double, cz double) {
    var expansion = Lightmap.CELL_SIZE / (Lightmap.CELL_SIZE - 1.0)

    _vertices.append(x)
    _vertices.append(y)
    _vertices.append(z)

    _uvs.append(s0)
    _uvs.append(t0)
    _uvs.append(s1)
    _uvs.append(t1)

    _updateUVs.append(s2)
    _updateUVs.append(t2)

    _updatePosition.append(cx + (x - cx) * expansion)
    _updatePosition.append(cy + (y - cy) * expansion)
    _updatePosition.append(cz + (z - cz) * expansion)
  }

  def generate {
    assert(_startX % SIZE_X == 0)
    assert(_startY % SIZE_Y == 0)
    assert(_startZ % SIZE_Z == 0)

    # Generate terrain
    for z in _startZ.._startZ + SIZE_Z {
      for x in _startX.._startX + SIZE_X {
        var bottom = 0
        var top = Math.round(SIZE_Y * 1.1 + SIZE_Y / 2 * Terrain.height(x * 0.02, z * 0.02)) as int
        var i = (x - _startX) + (z - _startZ) * SIZE_X * SIZE_Y

        for y in Math.max(bottom, _startY)..Math.min(top, _startY + SIZE_Y) {
          var cell Cell =
            y + 1 == top ? .GRASS :
            y < top - 3 ? .STONE :
            .DIRT
          data[i + (y - _startY) * SIZE_X] = cell
        }
      }
    }

    # Generate walkways
    if true {
      for level in 0..6 {
        var y = SIZE_Y * 2 - level * 10 - 1
        if y < _startY || y >= _startY + SIZE_Y {
          continue
        }
        for z in _startZ.._startZ + SIZE_Z {
          for x in _startX.._startX + SIZE_X {
            # Convert to barycentric coordinates
            var px = x * 0.01
            var py = z * 0.01
            py /= 0.866025404
            px -= 0.5 * py
            if (level & 1) != 0 {
              px -= 1.0 / 3.0
            }
            px -= Math.floor(px)
            py -= Math.floor(py)
            var pz = 1 - px - py
            if pz < 0 {
              px = 1 - px
              py = 1 - py
              pz = -pz
            }

            # Cut out hexagons
            var r = 2.0 / 3.0
            var t = 0.03
            var max = Math.max(Math.max(px, py), pz)
            var min = Math.min(Math.min(px, py), pz)
            if max > r + t || (max < r - t && min > t) {
              continue
            }
            data[(x - _startX) + ((y - _startY) + (z - _startZ) * SIZE_X) * SIZE_Y] = Cell.METAL
          }
        }
      }
    }

    invalidate
  }
}

namespace Chunk {
  const SHIFT_X = 5
  const SHIFT_Y = 5
  const SHIFT_Z = 5

  const SIZE_X = 1 << SHIFT_X
  const SIZE_Y = 1 << SHIFT_Y
  const SIZE_Z = 1 << SHIFT_Z

  const TEXTURE_INDEX = [
    [0, 0, 0], # EMPTY
    [2, 2, 2], # DIRT
    [0, 2, 3], # GRASS
    [1, 1, 1], # STONE
    [66, 2, 68], # SNOW
    [6, 6, 5], # METAL
  ]

  def _calcAO(side1 Cell, side2 Cell, corner Cell) double {
    if side1 != .EMPTY && side2 != .EMPTY { return 0.2 }
    if side1 != .EMPTY || side2 != .EMPTY || corner != .EMPTY { return 0.5 }
    return 1
  }
}
